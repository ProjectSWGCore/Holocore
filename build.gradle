import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
	id 'application'
	id 'java'
	id 'idea'
	id "com.github.johnrengelman.shadow" version "4.0.4"
	id "org.javamodularity.moduleplugin" version "1.4.0"
	id "org.beryx.jlink" version "2.4.0"
}

mainClassName = 'com.projectswg.holocore.ProjectSWG'
sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
	display { }
	utility { }
	integration { }
}

test {
	afterSuite { TestDescriptor td, TestResult tr ->
		if (td.parent == null) {
			println 'Tests run: (' + tr.getTestCount() + '), Failures: (' + tr.getFailedTestCount() + ')' // IMPORTANT - this is required for Lightspeed
		}
	}
}

idea {
    module {
        inheritOutputDirs = true
    }
}

jlink {
	imageDir.set(file("$buildDir/holocore"))
	imageZip.set(file("$buildDir/holocore.zip"))
	launcher {
		name = 'holocore'
		jvmArgs = []
		unixScriptTemplate = file('src/main/resources/jlink-unix-launch-template.txt')
	}
}

shadowJar {
	archiveBaseName.set("Holocore")
	archiveClassifier.set(null)
	archiveVersion.set(null)
}

repositories {
	jcenter()
}

dependencies {
	compile project(':pswgcommon')
	
	compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.23.1'
	compile group: 'org.mongodb', name: 'mongodb-driver-sync', version: '3.9.1'
	compile group: 'me.joshlarson', name: "fast-json", version: '3.0.0'
	compile group: 'me.joshlarson', name: 'jlcommon-network', version: '1.0.0'
	
	testCompile 'junit:junit:4.12'
	
	displayCompile project(':pswgcommon')
	displayCompile sourceSets.main.output
	
	utilityCompile project(':pswgcommon')
	utilityCompile sourceSets.main.output
	utilityCompile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.23.1'
	utilityCompile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.6.3'
	utilityCompile group: 'me.joshlarson', name: "fast-json", version: '3.0.0'
	
	integrationCompile project(':pswgcommon')
	integrationCompile project(':client-holocore')
	integrationCompile sourceSets.main.output
	integrationCompile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.23.1'
	integrationCompile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.6.3'
	integrationCompile 'junit:junit:4.12'
}

task CreateConvertLoginJar(type: ShadowJar) {
	archiveBaseName.set("ConvertLogin")
	archiveClassifier.set(null)
	archiveVersion.set(null)
	manifest.attributes 'Main-Class': 'com.projectswg.utility.ConvertLogin'
	from sourceSets.utility.output
	configurations = [project.configurations.utilityRuntime]
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}

task CreatePacketCaptureProcessor(type: ShadowJar) {
	archiveBaseName.set("PacketCaptureProcessor")
	archiveClassifier.set(null)
	archiveVersion.set(null)
	manifest.attributes 'Main-Class': 'com.projectswg.utility.packets.ProcessPacketCapture'
	from sourceSets.utility.output
	configurations = [project.configurations.utilityRuntime]
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}
